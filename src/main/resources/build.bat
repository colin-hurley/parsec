@echo off
rem This script is meant to run inside a DOSBox session. It invokes QuickBASIC
rem to compile the project source and produce an executable.

rem Note: Tse Maven ${*} property tokens used in this file will be replaced at
rem build time before the script is executed in DOSBox.

rem Note: Drive C will already be mounted with the build output directory where
rem this script is being run.

rem Remove any PARSED.EXE file produced in a previous build.
rem We can't make DOSBox exit with a nonzero code to signal Maven to fail the
rem build, so Maven will instead use the absence of PARSEC.EXE upon DOSBox
rem termination to determine that the QuickBASIC compilation has failed.
del C:\PARSEC.EXE

rem Mount the QuickBASIC directory
mount D "${qb.home}"

rem Note: These commands were obtained by opening the parsec.bas file in
rem QBX.EXE and building the PARSEC.EXE manually.

rem This BC.EXE command is essentially unmodified from the form used by QBX.EXE.
@echo on
D:\BC.EXE C:\PARSEC.BAS/E/O/Ot/G2/Fs/Lr/FPi/T/C:512;
@echo off
if errorlevel 1 goto :ERROR

rem This LINK.EXE command was reconstructed based on the temporary QBLNK.TMP
rem response file generated by QBX.EXE during the build.
@echo on
D:\LINK.EXE /EX C:\PARSEC.OBJ,C:\PARSEC.EXE,nul.map,D:\BCl71EFR.LIB;,
@echo off
if errorlevel 1 goto :ERROR

goto :EXIT

:ERROR
rem Display a failure banner and pause before exiting DOSBox so the user can
rem see what the problem was.
echo ========== BUILD FAILURE ==========
pause

:EXIT
